%% Problem
%% ---------------------
%% Each new term in the Fibonacci sequence is generated by adding the previous two terms.
%% By starting with 1 and 2, the first 10 terms will be:
%% 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
%%
%% Find the sum of all the even-valued terms in the sequence which do not exceed four million.
%% ---------------------

-module(p002).
-include_lib("eunit/include/eunit.hrl").

%% Solution
%% ---------------------
%% Streight forward
%% ---------------------

fib(Max) ->
    fib(1, 1, Max).

fib(Fst, Snd, Max) ->
    fib(Fst, Snd, Max, 0).

fib(_Fst, Snd, Max, Acc) when Snd > Max ->
    Acc;
fib(Fst, Snd, Max, Acc) when Snd rem 2 =:= 0 ->
    fib(Snd, Fst+Snd, Max, Acc+Snd);
fib(Fst, Snd, Max, Acc) ->
    fib(Snd, Fst+Snd, Max, Acc).

fib_test() ->
    ?assertEqual(4613732, fib(4000000)).

%% Solution
%% ---------------------
%% Found on Euler forum
%% ---------------------

fse(Max) -> fse({1, 1}, Max, 0).

fse({P1, P2}, Max, T) when (P1 + P2) < Max ->
    fse({P1 + 2 * P2, 2 * P1 + 3 * P2}, Max, T + P1 + P2);
fse({P1, P2}, Max, T) when (P1 + P2) >= Max -> T.

fse_test() ->
    ?assertEqual(4613732, fse(4000000)).

%% Solution
%% ---------------------
%% 2 8 34 144...
%% E(n) = 4*E(n-1) + E(n-2)
%% ---------------------

f(M) -> f(2, 8, M, 10).

f(A, B, Max, R) when A + 4*B > Max -> R;
f(A, B, Max, R) -> f(B, A + 4*B, Max, R + A + 4*B).

f_test() ->
    ?assertEqual(4613732, f(4000000)).
